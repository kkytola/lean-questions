/-
Copyright (c) 2021 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import tactic
import analysis.normed_space.bounded_linear_maps
import analysis.normed_space.weak_dual
import analysis.seminorm
import linear_algebra.affine_space.basic

noncomputable theory
open normed_space
open metric
open basis
open set
open function
open filter
open_locale topological_space

section alaoglu
/-!
### Banach-Alaoglu theorem
We prove that the dual unit ball of a normed space `E` over either `â„` or `â„‚` is compact
in the weak-star topology. (In fact more generally any polar of a neighborhood of the origin
is compact in `weak_dual ğ•œ E` for `[is_R_or_C ğ•œ]`.)
-/

-- Where to place?
lemma linear_map_bound_of_unit_sphere_bound
  {ğ•œ : Type*} [is_R_or_C ğ•œ] {E : Type*} [normed_group E] [normed_space ğ•œ E]
  (c : â„) (f : E â†’â‚—[ğ•œ] ğ•œ)
  (h : âˆ€ z âˆˆ sphere (0 : E) 1, âˆ¥ f z âˆ¥ â‰¤ c) (z : E) : âˆ¥ f z âˆ¥ â‰¤ c * âˆ¥ z âˆ¥ :=
begin
  by_cases z_zero : z = 0,
  { rw z_zero, simp only [linear_map.map_zero, norm_zero, mul_zero], },
  set zâ‚ := (âˆ¥ z âˆ¥â»Â¹ : ğ•œ) â€¢ z with hzâ‚,
  have norm_f_zâ‚ : âˆ¥ f zâ‚ âˆ¥ â‰¤ c,
  { exact h zâ‚ (by simp only [norm_smul_inv_norm z_zero, mem_sphere_zero_iff_norm]), },
  have eq : f z = âˆ¥ z âˆ¥ * (f zâ‚),
  { rw [hzâ‚, linear_map.map_smul, smul_eq_mul, â†mul_assoc, mul_inv_cancel, one_mul],
    simp only [z_zero, is_R_or_C.of_real_eq_zero, norm_eq_zero, ne.def, not_false_iff], },
  rw [eq, normed_field.norm_mul, is_R_or_C.norm_eq_abs, is_R_or_C.abs_of_real, 
      abs_norm_eq_norm, mul_comm],
  apply mul_le_mul norm_f_zâ‚ rfl.ge (norm_nonneg z) ((norm_nonneg (f zâ‚)).trans norm_f_zâ‚),
end

-- Where to place?
lemma linear_map_bound_of_sphere_bound
  {ğ•œ : Type*} [is_R_or_C ğ•œ] {E : Type*} [normed_group E] [normed_space ğ•œ E]
  {r : â„} (r_pos : 0 < r) (c : â„) (f : E â†’â‚—[ğ•œ] ğ•œ)
  (h : âˆ€ z âˆˆ sphere (0 : E) r, âˆ¥ f z âˆ¥ â‰¤ c) (z : E) : âˆ¥ f z âˆ¥ â‰¤ c / r * âˆ¥ z âˆ¥ :=
begin
  by_cases z_zero : z = 0,
  { rw z_zero, simp only [linear_map.map_zero, norm_zero, mul_zero], },
  have norm_z_eq : âˆ¥(âˆ¥ z âˆ¥ : ğ•œ)âˆ¥ =  âˆ¥ z âˆ¥ := norm_norm' ğ•œ E z,
  have norm_r_eq : âˆ¥(r : ğ•œ)âˆ¥ = r,
  { rw [is_R_or_C.norm_eq_abs, is_R_or_C.abs_of_real],
    exact abs_of_pos r_pos, },
  set zâ‚ := (r * âˆ¥ z âˆ¥â»Â¹ : ğ•œ) â€¢ z with hzâ‚,
  have norm_f_zâ‚ : âˆ¥ f zâ‚ âˆ¥ â‰¤ c,
  { apply h zâ‚,
    rw [mem_sphere_zero_iff_norm, hzâ‚, norm_smul, normed_field.norm_mul],
    simp only [normed_field.norm_inv],
    rw [norm_z_eq, mul_assoc, inv_mul_cancel (norm_pos_iff.mpr z_zero).ne.symm, mul_one],
    unfold_coes,
    simp only [norm_algebra_map_eq, ring_hom.to_fun_eq_coe],
    exact abs_of_pos r_pos, },
  have r_ne_zero : (r : ğ•œ) â‰  0 := (algebra_map â„ ğ•œ).map_ne_zero.mpr r_pos.ne.symm,
  have eq : f z = âˆ¥ z âˆ¥ / r * (f zâ‚),
  { rw [hzâ‚, linear_map.map_smul, smul_eq_mul],
    rw [â† mul_assoc, â† mul_assoc, div_mul_cancel _ r_ne_zero, mul_inv_cancel, one_mul],
    simp only [z_zero, is_R_or_C.of_real_eq_zero, norm_eq_zero, ne.def, not_false_iff], },
  rw [eq, normed_field.norm_mul, normed_field.norm_div, norm_z_eq, norm_r_eq,
      div_mul_eq_mul_div, div_mul_eq_mul_div, mul_comm],
  apply div_le_div _ _ r_pos rfl.ge,
  { exact mul_nonneg ((norm_nonneg _).trans norm_f_zâ‚) (norm_nonneg z), },
  apply mul_le_mul norm_f_zâ‚ rfl.le (norm_nonneg z) ((norm_nonneg _).trans norm_f_zâ‚),
end

-- Where to place?
lemma linear_map_bound_of_unit_ball_bound
  {ğ•œ : Type*} [is_R_or_C ğ•œ] {E : Type*} [normed_group E] [normed_space ğ•œ E]
  (c : â„) (f : E â†’â‚—[ğ•œ] ğ•œ) 
  (h : âˆ€ z âˆˆ closed_ball (0 : E) 1, âˆ¥ f z âˆ¥ â‰¤ c) : 
  âˆ€ (z : E), âˆ¥ f z âˆ¥ â‰¤ c * âˆ¥ z âˆ¥ :=
begin
  apply linear_map_bound_of_unit_sphere_bound c f,
  exact Î» z hz, h z hz.le,
end

-- Where to place? `analysis/normed_space/operator_norm`?
lemma linear_map_bound_of_ball_bound
  {ğ•œ : Type*} [is_R_or_C ğ•œ] {E : Type*} [normed_group E] [normed_space ğ•œ E]
  {r : â„} (r_pos : 0 < r) (c : â„) (f : E â†’â‚—[ğ•œ] ğ•œ) 
  (h : âˆ€ z âˆˆ closed_ball (0 : E) r, âˆ¥ f z âˆ¥ â‰¤ c) : 
  âˆ€ (z : E), âˆ¥ f z âˆ¥ â‰¤ c / r * âˆ¥ z âˆ¥ :=
begin
  apply linear_map_bound_of_sphere_bound r_pos c f,
  exact Î» z hz, h z hz.le,
end

-- Where to place? `analysis/normed_space/operator_norm`?
lemma _root_.continuous_linear_map.op_norm_bound_of_ball_bound
  {ğ•œ : Type*} [is_R_or_C ğ•œ] {E : Type*} [normed_group E] [normed_space ğ•œ E]
  {r : â„} (r_pos : 0 < r) (c : â„) (f : E â†’L[ğ•œ] ğ•œ) 
  (h : âˆ€ z âˆˆ closed_ball (0 : E) r, âˆ¥ f z âˆ¥ â‰¤ c) : âˆ¥ f âˆ¥ â‰¤ c / r :=
begin
  apply continuous_linear_map.op_norm_le_bound,
  { apply div_nonneg _ r_pos.le,
    exact (norm_nonneg _).trans 
          (h 0 (by simp only [norm_zero, mem_closed_ball, dist_zero_left, r_pos.le])), },
  apply linear_map_bound_of_ball_bound r_pos,
  exact Î» z hz, h z hz,
end

variables {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
variables {E : Type*} [normed_group E] [normed_space ğ•œ E]

namespace weak_dual

/-- Given a subset `s` in a normed space `E` (over a field `ğ•œ`), the polar
`polar s` is the subset of `weak_dual ğ•œ E` consisting of those functionals which
evaluate to something of norm at most one at all points `z âˆˆ s`. -/
def polar (s : set E) : set (weak_dual ğ•œ E) :=
{x' : weak_dual ğ•œ E | âˆ€ z âˆˆ s, âˆ¥ x' z âˆ¥ â‰¤ 1 }

@[simp] lemma zero_mem_polar (s : set E) :
  (0 : weak_dual ğ•œ E) âˆˆ (@polar ğ•œ _ E _ _ s) :=
Î» _ _, by simp only [zero_le_one, continuous_linear_map.zero_apply, norm_zero]

lemma polar_eq_Inter (s : set E) :
  (@polar ğ•œ _ E _ _ s) = â‹‚ z âˆˆ s, {x' : weak_dual ğ•œ E | âˆ¥ x' z âˆ¥ â‰¤ 1 } :=
by { dunfold polar, ext, simp only [mem_bInter_iff, mem_set_of_eq], }

-- Exists already? If not, where to place? `topology/basic`?
lemma _root_.continuous.is_closed_preimage {Î± Î² : Type*} [topological_space Î±] [topological_space Î²]
  {f : Î± â†’ Î²} (f_cont : continuous f) {s : set Î²} (s_closed : is_closed s) :
  is_closed (fâ»Â¹' s) :=
begin
  apply is_open_compl_iff.mp,
  rw â† preimage_compl,
  exact f_cont.is_open_preimage _ (is_open_compl_iff.mpr s_closed),
end

/-- The polar `polar s` of a set `s : E` is a closed subset of `weak_dual ğ•œ E`. -/
lemma polar_closed (s : set E) : is_closed (@polar ğ•œ _ E _ _ s) :=
begin
  rw polar_eq_Inter,
  apply is_closed_bInter,
  intros z hz,
  have eq : {x' : weak_dual ğ•œ E | âˆ¥x' zâˆ¥ â‰¤ 1} = (Î» (x' : weak_dual ğ•œ E), âˆ¥x' zâˆ¥)â»Â¹' (Iic 1),
  by refl,
  rw eq,
  refine continuous.is_closed_preimage _ (is_closed_Iic),
  apply continuous.comp continuous_norm (eval_continuous _ _ z),
end

/-- If `x'` is a dual element such that the norms `âˆ¥ x' z âˆ¥` are bounded for `z âˆˆ s`, then a
small scalar multiple of `x'` is in `polar s`. -/
lemma smul_mem_polar {s : set E} {x' : weak_dual ğ•œ E} {c : ğ•œ}
  (hc : âˆ€ z, z âˆˆ s â†’ âˆ¥ x' z âˆ¥ â‰¤ âˆ¥câˆ¥) : (câ»Â¹ â€¢ x') âˆˆ (@polar ğ•œ _ E _ _ s) :=
begin
  by_cases c_zero : c = 0,
  { rw c_zero,
    dunfold polar, 
    simp only [zero_le_one, continuous_linear_map.zero_apply, norm_zero, 
               mem_set_of_eq, implies_true_iff, inv_zero, zero_smul], },
  have eq : âˆ€ z, âˆ¥ câ»Â¹ â€¢ (x' z) âˆ¥ = âˆ¥ câ»Â¹ âˆ¥ * âˆ¥ x' z âˆ¥ := Î» z, norm_smul câ»Â¹ _,
  have le : âˆ€ z, z âˆˆ s â†’ âˆ¥ câ»Â¹ â€¢ (x' z) âˆ¥ â‰¤ âˆ¥ câ»Â¹ âˆ¥ * âˆ¥ c âˆ¥,
  { intros z hzs,
    rw eq z,
    apply mul_le_mul (le_of_eq rfl) (hc z hzs) (norm_nonneg _) (norm_nonneg _), },
  have cancel : âˆ¥ câ»Â¹ âˆ¥ * âˆ¥ c âˆ¥ = 1,
  by simp only [c_zero, norm_eq_zero, ne.def, not_false_iff, 
                inv_mul_cancel, normed_field.norm_inv],
  rwa cancel at le,
end

/-- The `polar` of closed unit ball in a normed space `E` is the closed unit ball of the (normed) dual
(seen as a subset of the weak dual). -/
lemma polar_closed_unit_ball
  {ğ•œ : Type*} [is_R_or_C ğ•œ] {E : Type*} [normed_group E] [normed_space ğ•œ E] : 
  (@polar ğ•œ _ E _ _ (closed_ball (0 : E) 1)) 
    = {x' : weak_dual ğ•œ E | (âˆ¥ x'.to_normed_dual âˆ¥ â‰¤ 1) } :=
begin
  ext x',
  simp only [mem_closed_ball, mem_set_of_eq, dist_zero_right],
  split, 
  { intros h,
    apply continuous_linear_map.op_norm_le_of_ball zero_lt_one zero_le_one,
    exact Î» _ _, linear_map_bound_of_unit_ball_bound 1 x'.to_normed_dual.to_linear_map h _, },
  { intros h z hz,
    simp only [mem_closed_ball, dist_zero_right] at hz,
    apply (continuous_linear_map.unit_le_op_norm x'.to_normed_dual z hz).trans h, },
end

/-- If `s` is a neighborhood of the origin in a normed space `E`, then at any point `z : E` there
exists a bound for the norms of the values `x' z` of the elements `x' âˆˆ polar s` of the polar of `s`. -/
lemma polar_eval_bounded_of_nbhd_zero {s : set E} (s_nhd : s âˆˆ ğ“ (0 : E)) (z : E) :
  âˆƒ (r : â„), âˆ€ (x' : weak_dual ğ•œ E), x' âˆˆ @polar ğ•œ _ E _ _ s â†’ (âˆ¥ x' z âˆ¥ â‰¤ r) :=
begin
  have s_absnt : absorbent ğ•œ s := absorbent_nhds_zero s_nhd,
  rcases s_absnt z with âŸ¨c, âŸ¨c_pos, hcâŸ©âŸ©,
  cases normed_field.exists_lt_norm ğ•œ c with a ha,
  specialize hc a ha.le,
  have a_norm_pos : 0 < âˆ¥ a âˆ¥ := lt_trans c_pos ha,
  have a_ne_zero : a â‰  0 := norm_pos_iff.mp a_norm_pos,
  have w_in_s : aâ»Â¹ â€¢ z âˆˆ s,
  { rcases hc with âŸ¨ w , âŸ¨hws, hawâŸ©âŸ©,
    rwa [â† haw, â† mul_smul, inv_mul_cancel a_ne_zero, one_smul], }, 
  use âˆ¥aâˆ¥,
  intros x' hx',
  specialize hx' _ w_in_s,
  simp only [algebra.id.smul_eq_mul, normed_field.norm_mul, 
             continuous_linear_map.map_smul, normed_field.norm_inv] at hx',
  have key := mul_le_mul (@rfl _ âˆ¥ a âˆ¥).ge hx' _ (norm_nonneg a),
  rwa [mul_one, â† mul_assoc, mul_inv_cancel (ne_of_gt a_norm_pos), one_mul] at key,
  apply mul_nonneg _ (norm_nonneg _),
  simp only [inv_nonneg, norm_nonneg],
end

/-- If `s` is a neighborhood of the origin in a normed space `E`, then there exists a
function `r : E â†’ â„` such that for all elements `x' âˆˆ polar s` one has `âˆ¥ x' z âˆ¥ â‰¤ r(z)`. -/
lemma polar_finite_values_of_nbhd_zero {s : set E} (s_nhd : s âˆˆ ğ“ (0 : E)) :
  âˆƒ (r : E â†’ â„), âˆ€ (x' : weak_dual ğ•œ E) (z : E), x' âˆˆ (@polar ğ•œ _ E _ _ s) â†’ âˆ¥ x' z âˆ¥ â‰¤ r z :=
begin
  cases classical.axiom_of_choice (@polar_eval_bounded_of_nbhd_zero ğ•œ _ E _ _ s s_nhd) with r hr,
  use r,
  intros x' z,
  exact hr z x',
end

/-- Given a neighborhood `s` of the origin in a normed space `E` over `â„` or `â„‚`, the dual norms 
of all elements of the polar `polar s` are bounded by a constant. -/
lemma polar_bounded_of_nbhd_zero
  {ğ•œ : Type*} [is_R_or_C ğ•œ] {E : Type*} [normed_group E] [normed_space ğ•œ E]
  {s : set E} (s_nhd : s âˆˆ ğ“ (0 : E)) :
  âˆƒ (c : â„), âˆ€ (x' : weak_dual ğ•œ E), x' âˆˆ @polar ğ•œ _ E _ _ s â†’ (âˆ¥ x'.to_normed_dual âˆ¥ â‰¤ c) :=
begin
  rcases metric.mem_nhds_iff.mp s_nhd with âŸ¨r, âŸ¨r_pos, r_ballâŸ©âŸ©,
  have half_r_pos : 0 < r / 2 := by linarith,
  use 2 / r,
  intros x' hx',
  have key := continuous_linear_map.op_norm_bound_of_ball_bound half_r_pos 1 x',
  simp only [one_div_div] at key,
  apply key,
  intros z hz,
  have z_mem_ball : z âˆˆ ball (0 : E) r,
  { simp only [mem_ball_zero_iff],
    simp only [mem_closed_ball, dist_zero_right] at hz,
    linarith, },
  exact hx' z (r_ball z_mem_ball),
end

/-- Given a neighborhood `s` of the origin in a normed space `E`, for any `z : E` it
is possible to choose a real number `r` such that for any functional `x' âˆˆ polar s` in
the polar of `s`, the value at `z` satisfies the norm bound `âˆ¥ x' z âˆ¥ â‰¤ r`. Such an `r`
is given by `bounds_fun _ z`. -/
private def bounds_fun {s : set E} (s_nhd : s âˆˆ ğ“ (0 : E)) : E â†’ â„ :=
classical.some (classical.axiom_of_choice (@polar_eval_bounded_of_nbhd_zero ğ•œ _ E _ _ s s_nhd))

private lemma bounds_fun_spec {s : set E} (s_nhd : s âˆˆ ğ“ (0 : E))
  (x' : weak_dual ğ•œ E) (z : E) :
  x' âˆˆ @polar ğ•œ _ E _ _ s â†’ âˆ¥ x' z âˆ¥ â‰¤ @bounds_fun ğ•œ _ E _ _ s s_nhd z :=
classical.some_spec 
  (classical.axiom_of_choice (@polar_eval_bounded_of_nbhd_zero ğ•œ _ E _ _ s s_nhd)) z x'

/-- The (weak) dual `weak_dual ğ•œ E` of a normed space `E` consists of bounded linear
functionals `E â†’ ğ•œ`. Such functionals can be interpreted as elements of the Cartesian
product `Î  (_ : E), ğ•œ` via the function `weak_dual.to_Pi : weak_dual ğ•œ E â†’ Î  (_ : E), ğ•œ`. -/
def _root_.weak_dual.to_Pi {ğ•œ : Type*} [nondiscrete_normed_field ğ•œ]
  {E : Type*} [topological_space E] [add_comm_group E] [module ğ•œ E]
  (x' : weak_dual ğ•œ E) := ((Î» z, (x' z)) : (Î  (_ : E), ğ•œ))

/-- The function `weak_dual.to_Pi : weak_dual ğ•œ E â†’ Î  (_ : E), ğ•œ` is an embedding. -/
lemma embedding_weak_dual_to_pi : 
  embedding (Î» (x' : weak_dual ğ•œ E), x'.to_Pi) :=
{ induced := eq_of_nhds_eq_nhds (congr_fun rfl),
  inj := begin 
    intros Ï†â‚ Ï†â‚‚ h,
    ext z,
    exact congr_fun h z,
  end, }

lemma pi_ball_bounds_fun_cpt [proper_space ğ•œ] {s : set E} (s_nhd : s âˆˆ ğ“ (0 : E)) :
  is_compact (set.pi (univ : set E) (Î» z, (closed_ball (0 : ğ•œ) (@bounds_fun ğ•œ _ E _ _ s s_nhd z)))) :=
begin
  apply is_compact_univ_pi,
  exact Î» z, proper_space.is_compact_closed_ball 0 _,
end

/-- The image of the polar `polar s` of a neighborhood `s` of the origin under
`weak_dual.to_Pi : weak_dual ğ•œ E â†’ Î  (_ : E), ğ•œ` is contained in a product of closed balls. -/
lemma embedding_weak_dual_to_pi.image_polar_nhd_subset {s : set E} (s_nhd : s âˆˆ ğ“ (0 : E)) :
  (@weak_dual.to_Pi ğ•œ _ E _ _ _) '' (polar s) âŠ† 
    (set.pi (univ : set E) (Î» z, (closed_ball (0 : ğ•œ) (@bounds_fun ğ•œ _ E _ _ s s_nhd z)))) :=
begin
  intros f hf,
  simp at hf,
  rcases hf with âŸ¨x', hx', f_eqâŸ©,
  simp only [mem_closed_ball, dist_zero_right, mem_univ_pi],
  intros z,
  have key := bounds_fun_spec s_nhd x' z,
  have eq : x' z = f z := congr_fun f_eq z,
  rw eq at key,
  exact key hx',
end

/-- In the product of copies of a normed field, sets of the form `{g | âˆ¥ f(i) - g(i) âˆ¥ < Îµ}` for
`Îµ > 0` are neighborhoods of `f`. -/
lemma basic_nhd_of_Pi_normed_field {Î¹ : Type*} 
  (f : (Î  (_ : Î¹), ğ•œ)) (i : Î¹) {Îµ : â„} (Îµ_pos : 0 < Îµ) :
  {g : (Î  (_ : Î¹), ğ•œ) | âˆ¥ f i - g i âˆ¥ < Îµ} âˆˆ ğ“ f :=
begin
  have eq : { g : (Î  (_ : Î¹), ğ•œ) | âˆ¥ f i - g i âˆ¥ < Îµ}
            = set.pi ({i} : set Î¹) (Î» _, ball (f i) Îµ),
  { ext g,
    simp only [mem_ball, eval_apply, singleton_pi, mem_set_of_eq, mem_preimage],
    rw dist_comm,
    exact mem_ball_iff_norm.symm, },
  rw eq,
  apply set_pi_mem_nhds,
  exact finite_singleton i,
  intros j hj,
  have eqâ‚€ : j = i := hj,
  rw eqâ‚€,
  exact ball_mem_nhds (f i) Îµ_pos,
end

/-- Elements of the closure of the range of the embedding
`weak_dual.to_Pi : weak_dual ğ•œ E â†’ Î  (_ : E), ğ•œ` are linear. Here it is stated as the elements
respecting linear combinations. -/
lemma embedding_weak_dual_to_pi.linear_of_mem_closure_range'
  (f : (Î  (_ : E), ğ•œ)) (hf : f âˆˆ closure (range (@weak_dual.to_Pi ğ•œ _ E _ _ _)))
  (câ‚ câ‚‚ : ğ•œ) (zâ‚ zâ‚‚ : E) : f (câ‚ â€¢ zâ‚ + câ‚‚ â€¢ zâ‚‚) = câ‚ â€¢ f(zâ‚) + câ‚‚ â€¢ f(zâ‚‚) :=
begin
  set Ï† : (Î  (_ : E), ğ•œ) â†’ ğ•œ := (Î» g, g (câ‚ â€¢ zâ‚ + câ‚‚ â€¢ zâ‚‚) - câ‚ â€¢ g(zâ‚) - câ‚‚ â€¢ g(zâ‚‚)) with hÏ†,
  have Ï†_cont : continuous Ï†,
  { apply continuous.sub,
    { apply continuous.sub,
      { exact continuous_apply (câ‚ â€¢ zâ‚ + câ‚‚ â€¢ zâ‚‚), },
      exact continuous.smul continuous_const (continuous_apply _), },
    exact continuous.smul continuous_const (continuous_apply _), },
  have sin_closed : is_closed ({0} : set ğ•œ) := t1_space.t1 0,
  have preim_cl : is_closed (Ï†â»Â¹' ({0} : set ğ•œ)) := Ï†_cont.is_closed_preimage sin_closed,
  suffices : range (@weak_dual.to_Pi ğ•œ _ E _ _ _) âŠ† Ï†â»Â¹' ({0} : set ğ•œ),
  { have key := (is_closed.closure_subset_iff preim_cl).mpr this hf,
    exact sub_eq_iff_eq_add'.mp (sub_eq_zero.mp key), },
  intros g hg,
  cases hg with gâ‚€ hgâ‚€,
  simp only [algebra.id.smul_eq_mul, mem_singleton_iff, norm_eq_zero, mem_preimage],
  rw [hÏ†, â† hgâ‚€],
  dunfold weak_dual.to_Pi,
  rw add_comm,
  simp only [algebra.id.smul_eq_mul, continuous_linear_map.map_add, add_sub_cancel, 
             sub_self, continuous_linear_map.map_smul],
end

/-- Elements of the closure of the range of the embedding
`weak_dual.to_Pi : weak_dual ğ•œ E â†’ Î  (_ : E), ğ•œ` can be viewed as linear maps `E â†’ ğ•œ`. -/
def embedding_weak_dual_to_pi.linear_of_mem_closure_range
  (f : (Î  (_ : E), ğ•œ)) (hf : f âˆˆ closure (range (@weak_dual.to_Pi ğ•œ _ E _ _ _))) :
  E â†’â‚—[ğ•œ] ğ•œ :=
{ to_fun := f,
  map_add' := begin
    intros zâ‚ zâ‚‚,
    have key := embedding_weak_dual_to_pi.linear_of_mem_closure_range' f hf (1 : ğ•œ) (1 : ğ•œ) zâ‚ zâ‚‚,
    rwa [one_smul, one_smul, one_smul ğ•œ _, one_smul ğ•œ _] at key,
  end,
  map_smul' := begin
    intros c z,
    have key := embedding_weak_dual_to_pi.linear_of_mem_closure_range' f hf c (0 : ğ•œ) z (0 : E),
    rwa [zero_smul, zero_smul, add_zero, add_zero] at key,
  end, }

lemma embedding_weak_dual_to_pi.linear_of_mem_closure_range_apply
  (f : (Î  (_ : E), ğ•œ)) (hf : f âˆˆ closure (range (@weak_dual.to_Pi ğ•œ _ E _ _ _))) (z : E) :
  embedding_weak_dual_to_pi.linear_of_mem_closure_range f hf z = f z := rfl

/-- Elements of the closure of the image under `weak_dual.to_Pi : weak_dual ğ•œ E â†’ Î  (_ : E), ğ•œ` of
a subset defined by a non-strict bound on the norm still satisfy the same bound. -/
lemma embedding_weak_dual_to_pi.norm_eval_le_of_mem_closure_norm_eval_le
  (z : E) (c : â„) (f : (Î  (_ : E), ğ•œ))
  (hf : f âˆˆ closure ((@weak_dual.to_Pi ğ•œ _ E _ _ _) '' {x' : weak_dual ğ•œ E | âˆ¥ x' z âˆ¥ â‰¤ c})) : 
  âˆ¥ f z âˆ¥ â‰¤ c :=
begin
  suffices : âˆ€ (Îµ : â„), 0 < Îµ â†’ âˆ¥ f (z) âˆ¥ â‰¤ c + Îµ,
  { exact le_of_forall_pos_le_add this, },
  intros Îµ Îµ_pos,
  have nhd := basic_nhd_of_Pi_normed_field f z Îµ_pos,
  have clos := mem_closure_iff_nhds.mp hf _ nhd,
  cases clos with g hg,
  simp only [mem_image, mem_inter_eq, mem_set_of_eq] at hg,
  rcases hg with âŸ¨tri, âŸ¨y', âŸ¨at_z_le, eq_gâŸ©âŸ©âŸ©,
  have eq : y'.to_Pi z = y' z := rfl,
  rw [â† eq_g, eq] at tri,  
  have key := norm_add_le_of_le tri.le at_z_le,
  rwa [sub_add_cancel, add_comm] at key,
end

/-- Elements of the closure of the image under `weak_dual.to_Pi : weak_dual ğ•œ E â†’ Î  (_ : E), ğ•œ` of
a polar `polar s` of a neighborhood `s` of the origin are continuous (linear) functions. -/
lemma embedding_weak_dual_to_pi.continuous_of_mem_closure_polar_nhd
  {ğ•œ : Type*} [is_R_or_C ğ•œ] {E : Type*} [normed_group E] [normed_space ğ•œ E]
  {s : set E} (s_nhd : s âˆˆ ğ“ (0 : E)) (Ï† : (Î  (_ : E), ğ•œ))
  (hÏ† : Ï† âˆˆ closure ((@weak_dual.to_Pi ğ•œ _ E _ _ _) '' (@polar ğ•œ _ E _ _ s))) :
  @continuous E ğ•œ _ _ Ï† :=
begin
  cases @polar_bounded_of_nbhd_zero ğ•œ _ E _ _ s s_nhd with c hc,
  have c_nn : 0 â‰¤ c := le_trans (norm_nonneg _) (hc 0 (zero_mem_polar s)),
  have hÏ†' : Ï† âˆˆ closure (range (@weak_dual.to_Pi ğ•œ _ E _ _ _)),
  { apply mem_of_mem_of_subset hÏ† _,
    apply closure_mono,
    simp only [preimage_range, subset_univ, image_subset_iff], },
  set flin := embedding_weak_dual_to_pi.linear_of_mem_closure_range Ï† hÏ†' with hflin,
  suffices : continuous flin,
  { assumption, },
  apply linear_map.continuous_of_bound flin c,
  intros z,
  set Î¸ := Î» (Ïˆ : Î  (_ : E), ğ•œ), âˆ¥ Ïˆ z âˆ¥ with hÎ¸,
  have Î¸_cont : continuous Î¸,
  { apply continuous.comp continuous_norm,
    exact continuous_apply z, },
  have sin_closed : is_closed (Icc (-c * âˆ¥zâˆ¥) (c * âˆ¥zâˆ¥) : set â„) := is_closed_Icc,
  have preim_cl := Î¸_cont.is_closed_preimage sin_closed,
  suffices : 
    (@weak_dual.to_Pi ğ•œ _ E _ _ _) '' (@polar ğ•œ _ E _ _ s) âŠ† Î¸â»Â¹' (Icc (-c * âˆ¥zâˆ¥) (c * âˆ¥zâˆ¥)),
  { exact ((is_closed.closure_subset_iff preim_cl).mpr this hÏ†).right, },
  intros Ïˆ hÏˆ,
  rcases hÏˆ with âŸ¨x', âŸ¨polar_x', Ïˆ_x'âŸ©âŸ©,
  rw â† Ïˆ_x',
  simp only [neg_mul_eq_neg_mul_symm, mem_preimage, mem_Icc, hÎ¸],
  split,
  { apply le_trans _ (norm_nonneg _),
    rw right.neg_nonpos_iff,
    exact mul_nonneg c_nn (norm_nonneg _), },
  apply le_trans (continuous_linear_map.le_op_norm x' z) _,
  exact mul_le_mul (hc x' polar_x') rfl.ge (norm_nonneg z) c_nn,
end

/-- The image under `weak_dual.to_Pi : weak_dual ğ•œ E â†’ Î  (_ : E), ğ•œ` of a polar `polar s` of a
neighborhood `s` of the origin is a closed set. -/
lemma embedding_weak_dual_to_pi.image_polar_nhd_closed
  {ğ•œ : Type*} [is_R_or_C ğ•œ] {E : Type*} [normed_group E] [normed_space ğ•œ E]
  {s : set E} (s_nhd : s âˆˆ ğ“ (0 : E)) :
  is_closed ((@weak_dual.to_Pi ğ•œ _ E _ _ _) '' (@polar ğ•œ _ E _ _ s)) :=
begin
  apply is_closed_iff_cluster_pt.mpr,
  intros f hf,
  simp only [mem_image, mem_set_of_eq],
  have f_in_closure : f âˆˆ closure ((@weak_dual.to_Pi ğ•œ _ E _ _ _) '' (@polar ğ•œ _ E _ _ s)),
  from mem_closure_iff_cluster_pt.mpr hf,
  have f_in_closureâ‚€ : f âˆˆ closure (range (@weak_dual.to_Pi ğ•œ _ E _ _ _)),
  { apply closure_mono (image_subset_range _ _),
    exact mem_closure_iff_cluster_pt.mpr hf, },
  set f_lin := embedding_weak_dual_to_pi.linear_of_mem_closure_range f f_in_closureâ‚€ with h_f_lin,
  have f_cont := embedding_weak_dual_to_pi.continuous_of_mem_closure_polar_nhd s_nhd f f_in_closure,
  set Ï† : weak_dual ğ•œ E := 
    { to_fun := f,
      map_add' := begin
        intros zâ‚ zâ‚‚,
        have key := f_lin.map_add zâ‚ zâ‚‚,
        rw h_f_lin at key,
        repeat {rwa embedding_weak_dual_to_pi.linear_of_mem_closure_range_apply 
          f f_in_closureâ‚€ _ at key, },
      end,
      map_smul' := begin
        intros c z,
        have key := f_lin.map_smul c z,
        rw h_f_lin at key,
        repeat {rwa embedding_weak_dual_to_pi.linear_of_mem_closure_range_apply 
          f f_in_closureâ‚€ _ at key, },
      end,
      cont := f_cont, } with hÏ†,
  use Ï†,
  split,
  { dunfold polar,
    simp,
    intros z hz,
    apply embedding_weak_dual_to_pi.norm_eval_le_of_mem_closure_norm_eval_le z 1 f,
    have ss : polar s âŠ† {x' : weak_dual ğ•œ E | âˆ¥x' zâˆ¥ â‰¤ 1},
    { intros x' hx',
      exact hx' z hz, },
    apply closure_mono (image_subset _ ss),
    exact mem_closure_iff_cluster_pt.mpr hf, },
  { ext z,
    dunfold to_Pi,
    rw hÏ†,
    simp, },
end

/-- The image under `weak_dual.to_Pi : weak_dual ğ•œ E â†’ Î  (_ : E), ğ•œ` of the polar `polar s` of
a neighborhood `s` of the origin is compact. -/
lemma embedding_weak_dual_to_pi.image_polar_nhd_compact
  {ğ•œ : Type*} [is_R_or_C ğ•œ] {E : Type*} [normed_group E] [normed_space ğ•œ E]
  {s : set E} (s_nhd : s âˆˆ ğ“ (0 : E)) :
  is_compact ((@weak_dual.to_Pi ğ•œ _ E _ _ _) '' (polar s)) :=
begin
  apply compact_of_is_closed_subset _ _ (embedding_weak_dual_to_pi.image_polar_nhd_subset s_nhd),
  exact pi_ball_bounds_fun_cpt s_nhd,
  exact embedding_weak_dual_to_pi.image_polar_nhd_closed s_nhd,
end

/-- The Banach-Alaoglu theorem: the polar `polar s` of a neighborhood `s` of the origin in a
normed space `E` over `ğ•œ` is compact subset of `weak_dual ğ•œ E` (assuming `[is_R_or_C ğ•œ]`). -/
theorem polar_nhd_weak_star_compact
  {ğ•œ : Type*} [is_R_or_C ğ•œ] {E : Type*} [normed_group E] [normed_space ğ•œ E]
  {s : set E} (s_nhd : s âˆˆ ğ“ (0 : E)) : is_compact (@polar ğ•œ _ E _ _ s) :=
begin
  apply (@embedding_weak_dual_to_pi ğ•œ _ E _ _ ).is_compact_iff_is_compact_image.mpr,
  exact embedding_weak_dual_to_pi.image_polar_nhd_compact s_nhd,
end

/-- The Banach-Alaoglu theorem: the dual unit ball is compact in the weak-star topology. -/
theorem unit_ball_weak_star_compact
  {ğ•œ : Type*} [is_R_or_C ğ•œ] {E : Type*} [normed_group E] [normed_space ğ•œ E] :
  is_compact {x' : weak_dual ğ•œ E | (âˆ¥ x'.to_normed_dual âˆ¥ â‰¤ 1)} :=
begin
  rw â† polar_closed_unit_ball,
  apply polar_nhd_weak_star_compact (closed_ball_mem_nhds (0 : E) (@zero_lt_one â„ _ _)),
end


end weak_dual

end alaoglu