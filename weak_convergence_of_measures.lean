/-
Copyright (c) 2021 Kalle Kyt√∂l√§. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle Kyt√∂l√§
-/
import tactic
import weak_dual
import measure_theory.integral.bochner


noncomputable theory
open measure_theory
open set
open filter
open_locale topological_space bounded_continuous_function ennreal nnreal

section finite_measures
/-!
### Finite measures
In this section, we define the `Type` of finite measures on a given measurable space. This type
will be equipped with the topology of weak convergence of measures when the measurable space is
a topological space equipped with its Borel sigma-algebra.
-/

variables {Œ± : Type} [measurable_space Œ±]

/-- Finite measures are defined as the subtype of measures that have the property of being finite
measures (i.e., their total mass is finite). -/
def finite_measures (Œ± : Type*) [measurable_space Œ±] : Type :=
{ Œº : measure Œ± // finite_measure Œº }

namespace finite_measures

instance has_zero {Œ± : Type*} [measurable_space Œ±] : 
  has_zero (finite_measures Œ±) :=
‚ü®{ val := 0,
   property := measure_theory.finite_measure_zero, }‚ü©

instance finite_measure_add {Œ± : Type*} [measurable_space Œ±]
  (Œº ŒΩ : measure Œ±) [finite_measure Œº] [finite_measure ŒΩ] :
  finite_measure (Œº + ŒΩ) :=
{ measure_univ_lt_top := begin 
    simp only [measure.coe_add, pi.add_apply, ennreal.add_lt_top],
    exact ‚ü®measure_lt_top Œº univ, measure_lt_top ŒΩ univ‚ü©,
  end, }

instance {Œ± : Type*} [measurable_space Œ±] : has_add (finite_measures Œ±) :=
{ add := (Œª (Œº ŒΩ : finite_measures Œ±), 
  { val := Œº.val + ŒΩ.val,
    property := @finite_measures.finite_measure_add _ _ _ _ Œº.prop ŒΩ.prop, }), }

instance : inhabited (finite_measures Œ±) := { default := 0 }

/-- A finite measure can be interpreted as a measure. -/
def to_measure (Œº : finite_measures Œ±) : (measure_theory.measure Œ±) := subtype.val Œº

instance to_measure.finite_measure (Œº : finite_measures Œ±) :
  finite_measure Œº.to_measure := Œº.prop

instance (Œ± : Type*) [measurable_space Œ±] :
  has_coe_to_fun (finite_measures Œ±) := ‚ü®(Œª _, set Œ± ‚Üí ‚Ñù‚â•0), 
    (Œª Œº, (Œª s, (Œº.val.measure_of s).to_nnreal))‚ü©

lemma to_fun_eq_to_measure_to_nnreal (ŒΩ : finite_measures Œ±) :
  (ŒΩ : set Œ± ‚Üí ‚Ñù‚â•0) = Œª s, (ŒΩ.to_measure s).to_nnreal := rfl

lemma to_measure_eq_val (ŒΩ : finite_measures Œ±) : ŒΩ.to_measure = ŒΩ.val := rfl

lemma to_measure_injective : 
  function.injective (@finite_measures.to_measure Œ± ‚Äπmeasurable_space Œ±‚Ä∫) :=
by { intros Œº ŒΩ, exact subtype.eq, }

lemma to_measure_zero :
  (@finite_measures.to_measure Œ± ‚Äπmeasurable_space Œ±‚Ä∫) 0 = 0 := rfl

lemma to_measure_add (Œº ŒΩ : finite_measures Œ±) :
  Œº.to_measure + ŒΩ.to_measure = (Œº + ŒΩ).to_measure := rfl

/-- The (total) mass of a finite measure `Œº` is `Œº univ`, i.e., the cast to `nnreal` of
`Œº.to_measure univ`. -/
def mass {Œ± : Type*} [measurable_space Œ±] (Œº : finite_measures Œ±) : ‚Ñù‚â•0 := Œº univ

lemma mass_def {Œ± : Type*} [measurable_space Œ±] {Œº : finite_measures Œ±} :
  Œº.mass = Œº univ := rfl

@[simp] lemma mass_ennreal {Œ± : Type*} [measurable_space Œ±]
  {Œº : finite_measures Œ±} : (Œº.mass : ‚Ñù‚â•0‚àû) = Œº.to_measure univ :=
begin
  apply ennreal.coe_to_nnreal,
  exact ennreal.lt_top_iff_ne_top.mp (Œº.prop).measure_univ_lt_top,
end

instance {Œ± : Type*} [measurable_space Œ±] : 
  add_comm_monoid (finite_measures Œ±) :=
(@finite_measures.to_measure_injective Œ± ‚Äπmeasurable_space Œ±‚Ä∫).add_comm_monoid
    (@finite_measures.to_measure Œ± ‚Äπmeasurable_space Œ±‚Ä∫)
    finite_measures.to_measure_zero finite_measures.to_measure_add

--TODO: Make `finite_measures Œ±` an `‚Ñù‚â•0`-module, so that it makes sense (later)
--      to define a continuous linear map `finite_measures Œ± ‚Üí weak_dual ‚Ñù‚â•0 (Œ± ‚Üí·µá ‚Ñù‚â•0)`.
--instance finite_measures.module : module ‚Ñù‚â•0 (finite_measures Œ±) := sorry

end finite_measures -- end namespace

end finite_measures -- end section



section probability_measures
/-!
### Probability measures
In this section, we define the `Type` of probability measures on a given measurable space. This
type will be equipped with the topology of weak convergence of measures when the measurable space
is a topological space equipped with its Borel sigma-algebra.

There is a coercion (embedding) to finite measures on the same space.
-/

variables {Œ± : Type} [measurable_space Œ±]

/-- Probability measures are defined as the subtype of measures that have the property of being
probability measures (i.e., their total mass is one). -/
def probability_measures (Œ± : Type) [measurable_space Œ±] : Type :=
{Œº : measure Œ± // probability_measure Œº}

namespace probability_measures

instance [inhabited Œ±] : inhabited (probability_measures Œ±) :=
‚ü®{ val := measure_theory.measure.dirac (default Œ±),
   property := measure_theory.measure.dirac.probability_measure, }‚ü©

/-- A probability measure can be interpreted as a measure. -/
def to_measure (Œº : probability_measures Œ±) : (measure_theory.measure Œ±) := Œº.val

instance to_measure.probability_measure (Œº : probability_measures Œ±) :
  probability_measure Œº.to_measure := Œº.prop

instance (Œ± : Type*) [measurable_space Œ±] :
  has_coe_to_fun (probability_measures Œ±) := ‚ü®(Œª _, set Œ± ‚Üí ‚Ñù‚â•0), 
    (Œª Œº, (Œª s, (Œº.val.measure_of s).to_nnreal))‚ü©

lemma to_fun_eq_to_measure_to_nnreal (ŒΩ : probability_measures Œ±) :
  (ŒΩ : set Œ± ‚Üí ‚Ñù‚â•0) = Œª s, (ŒΩ.to_measure s).to_nnreal := rfl

@[simp]
lemma to_fun_univ (ŒΩ : probability_measures Œ±) : ŒΩ univ = 1 :=
begin
  rw [to_fun_eq_to_measure_to_nnreal, ‚Üêennreal.one_to_nnreal],
  exact congr_arg ennreal.to_nnreal ŒΩ.prop.measure_univ,
end

lemma to_measure_eq_val (ŒΩ : probability_measures Œ±) : ŒΩ.to_measure = ŒΩ.val := rfl

lemma to_measure_injective : 
  function.injective (@probability_measures.to_measure Œ± ‚Äπmeasurable_space Œ±‚Ä∫) :=
by { intros Œº ŒΩ, exact subtype.eq, }

instance has_coe_to_finite_measures (Œ± : Type*) [measurable_space Œ±] :
  has_coe (probability_measures Œ±) (finite_measures Œ±) :=
{ coe := Œª Œº , { val := Œº.val,
                 property := begin
                   have key : (1 : ennreal) < ‚ä§ := ennreal.one_lt_top,
                   rw [‚ÜêŒº.prop.measure_univ] at key,
                   exact ‚ü®key‚ü©,
                 end, }}

/-- A probability measure can be interpreted as a finite measure. -/
def to_finite_measure (Œº : probability_measures Œ±) : (finite_measures Œ±) := Œº

@[simp]
lemma to_finite_measure_to_measure_eq_to_measure (ŒΩ : probability_measures Œ±) :
  (ŒΩ.to_finite_measure).to_measure = ŒΩ.to_measure := rfl

lemma to_finite_measure_to_measure_eq_val (ŒΩ : probability_measures Œ±) :
  (ŒΩ.to_finite_measure).to_measure = ŒΩ.val := rfl

@[simp]
lemma to_finite_measure_to_fun_eq_to_fun (ŒΩ : probability_measures Œ±) :
  (ŒΩ.to_finite_measure : set Œ± ‚Üí ‚Ñù‚â•0) = (ŒΩ : set Œ± ‚Üí ‚Ñù‚â•0) := rfl

@[simp]
lemma to_finite_measure_mass (Œº : probability_measures Œ±) :
  Œº.to_finite_measure.mass = 1 :=
begin
  unfold finite_measures.mass,
  rw [‚ÜêŒº.to_fun_univ, to_finite_measure_to_fun_eq_to_fun],
end

end probability_measures -- end namespace

end probability_measures -- end section



section various_lemmas

-- NOTE: Eh, what is the right way to do this `nnreal_mul_ennreal_to_nnreal`?
-- If this deserves to be added, then perhaps in `data.real.ennreal`?
-- It seems like a typical coercion issue to me, although it is only used once here.
lemma nnreal_mul_ennreal_to_nnreal (c : ‚Ñù‚â•0) (z : ‚Ñù‚â•0‚àû) :
  c * z.to_nnreal = ((c : ‚Ñù‚â•0‚àû) * z).to_nnreal :=
begin
  by_cases z_infty : z = ‚ä§,
  { rw z_infty,
    simp only [ennreal.top_to_nnreal, ennreal.to_nnreal_mul_top, mul_zero], },
  { have z_lt_top : z < ‚ä§ := ennreal.lt_top_iff_ne_top.mpr z_infty,
    simp only [ennreal.to_nnreal_mul, ennreal.to_nnreal_coe], },
end

-- NOTE: I have been almost wondering why the following is not the definition of
-- boundedness. I have not found it in the library, but believe it should be there.
-- Is `topology.continuous_function.bounded` the appropriate place? The proof needs golf, though.
lemma bounded_continuous_function.radius_bounded {Œ± Œ≤ : Type*}
  [topological_space Œ±] [metric_space Œ≤] (f : Œ± ‚Üí·µá Œ≤) (z : Œ≤):
  ‚àÉ (c : ‚Ñù), ‚àÄ (a : Œ±), dist z (f(a)) ‚â§ c :=
begin
  cases f.bounded with c hc ,
  by_cases is_empty Œ±,
  { use 0, intros a, exfalso, exact is_empty_iff.mp h a, },
  cases not_is_empty_iff.mp h with a‚ÇÄ,
  use (dist z (f(a‚ÇÄ))) + c,
  intros a,
  have le : ‚àÄ (y : ‚Ñù), y + (dist (f(a‚ÇÄ)) (f(a))) ‚â§ y + c := Œª y, add_le_add rfl.ge (hc a‚ÇÄ a),
  exact (dist_triangle z (f(a‚ÇÄ)) (f(a))).trans (le _),
end

-- NOTE: The following is a "special case" of the above. I think it is also worthwhile, although
-- it is not needed in this file. If it should be added, what is the right place? 
lemma bounded_continuous_function.norm_bounded {Œ± Œ≤ : Type*}
  [topological_space Œ±] [normed_group Œ≤] (f : Œ± ‚Üí·µá Œ≤) :
  ‚àÉ (c : ‚Ñù), ‚àÄ x, ‚à• f x ‚à• ‚â§ c :=
begin
  have norm_eq_dist : ‚àÄ (z : Œ≤), ‚à• z ‚à• = dist 0 z := Œª z, congr_fun (dist_zero_left).symm z,
  simp_rw [norm_eq_dist],
  exact bounded_continuous_function.radius_bounded f 0,
end

-- NOTE: The following is another "special case". While special, I think it may be worthwhile.
-- If so, what is the right place? How to golf?
lemma bounded_continuous_function.nnreal.upper_bound {Œ± : Type*} [topological_space Œ±]
  (f : Œ± ‚Üí·µá ‚Ñù‚â•0) : ‚àÉ (c : nnreal), (‚àÄ x, f(x) ‚â§ c) :=
begin
  have val_eq_dist_nnreal : ‚àÄ (z : ‚Ñù‚â•0), (z : ‚Ñù) = dist 0 z,
  { intros z,
    simp only [nnreal.dist_eq, nnreal.coe_zero, zero_sub, nnreal.abs_eq, abs_neg], },
  cases bounded_continuous_function.radius_bounded f 0 with c hc,
  simp_rw ‚Üêval_eq_dist_nnreal at hc,
  use (max c 0).to_nnreal,
  intros x,
  apply (@real.le_to_nnreal_iff_coe_le (f x) _ (le_max_right c 0)).mpr,
  apply (hc x).trans (le_max_left c 0),
end

-- NOTE: This one is not actually needed in the current version with nonnegative test functions.
-- It will be needed later. If this doesn't exist yet, then after golfing the proof, this might
-- be appropriate in `measure_theory.function.L1_space` (not in
-- `topology.continuous_function.bounded` because of imports?).
lemma bounded_continuous_function.integrable {Œ± Œ≤: Type}
  [topological_space Œ±] [measurable_space Œ±] [opens_measurable_space Œ±]
  [normed_group Œ≤] [measurable_space Œ≤] [borel_space Œ≤]
  (Œº : measure Œ±) [finite_measure Œº] (f : Œ± ‚Üí·µá Œ≤) : integrable f Œº :=
begin
  set f' := ennreal.of_real ‚àò norm ‚àò f with hf' ,
  suffices : lintegral Œº f' < ‚ä§ ,
  { have ae_mble : @ae_measurable Œ± Œ≤ ‚Äπmeasurable_space Œ≤‚Ä∫ ‚Äπmeasurable_space Œ±‚Ä∫ f Œº
    := continuous.ae_measurable f.continuous Œº,
    exact ‚ü® ae_mble , (has_finite_integral_iff_norm f).mpr this ‚ü©, } ,
  have bdd : ‚àÄ (x : Œ±), f' x ‚â§ ennreal.of_real (‚à• f ‚à•) ,
  { intros x,
    exact ennreal.of_real_le_of_real (bounded_continuous_function.norm_coe_le_norm f x), },
  have integr_bdd := @lintegral_mono Œ± _ Œº _ _ bdd,
  set c' := ennreal.of_real (‚à• f ‚à•) with hc',
  have const_integr : lintegral Œº (Œª x , c') = c' * (Œº(univ)) := by rw lintegral_const,
  have total : c' * (Œº(univ)) < ‚ä§,
  { apply ennreal.mul_lt_top ennreal.of_real_lt_top
      (‚Äπfinite_measure Œº‚Ä∫).measure_univ_lt_top, },
  rw ‚Üê const_integr at total,
  exact lt_of_le_of_lt integr_bdd total,
end

-- NOTE: I believe this is useful, but what is the right place?
-- `measure_theory.function.L1_space`? `topology.continuous_function.bounded`?
lemma bounded_continuous_function.coe_nnreal_comp_measurable {Œ± : Type*} [topological_space Œ±]
  [measurable_space Œ±] [opens_measurable_space Œ±] (f : Œ± ‚Üí·µá ‚Ñù‚â•0) : 
  measurable ((coe : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0‚àû) ‚àò f) :=
measurable.comp measurable_coe_nnreal_ennreal (continuous.measurable f.continuous)

-- NOTE: The following is probably too special for anywhere else, but is a useful standalone
-- lemma here.
lemma lintegral_lt_top_of_bounded_continuous_to_nnreal {Œ± : Type*} [topological_space Œ±]
  [measurable_space Œ±] (Œº : measure Œ±) [Œº_fin : finite_measure Œº] (f : Œ± ‚Üí·µá ‚Ñù‚â•0) :
  lintegral Œº ((coe : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0‚àû) ‚àò f) < ‚ä§ :=
begin
  cases bounded_continuous_function.nnreal.upper_bound f with c hc,
  have le' : (coe : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0‚àû) ‚àò f ‚â§ (Œª (x : Œ±), c),
  by { intros x, simp only [hc, ennreal.coe_le_coe], },
  apply lt_of_le_of_lt (@lintegral_mono _ _ Œº _ _ le'),
  rw lintegral_const,
  exact ennreal.mul_lt_top ennreal.coe_lt_top Œº_fin.measure_univ_lt_top,
end

-- TODO: What to do with this? Hmm... I've ended up using it quite regularly in the definitions
-- of certain topologies.
lemma tendsto_induced_iff {Œ± Œ≤ Œ≥ : Type*} {F : filter Œ≥} [topological_space Œ≤]
  {f : Œ± ‚Üí Œ≤} {xs : Œ≥ ‚Üí Œ±} {x : Œ±} :
  tendsto xs F (@nhds Œ± (topological_space.induced f infer_instance) x) ‚Üî
    tendsto (Œª i, f(xs(i))) F (ùìù (f(x))) :=
begin
  split,
  { intros conv_induced,
    have f_cont := @continuous_induced_dom Œ± Œ≤ f infer_instance,
    have key := @continuous.tendsto Œ± Œ≤
      (topological_space.induced f infer_instance) infer_instance f f_cont,
    exact tendsto.comp (key x) conv_induced, },
  { intros conv_image,
    rw nhds_induced,
    exact map_le_iff_le_comap.mp conv_image, },
end

end various_lemmas



section weak_convergence_of_measures

/-!
### The topology of weak convergence of measures
In this section, we define the topology of weak convergence on the set of Borel probability
measures and on the set of finite Borel measures on a topological space.
-/

/- NOTE / TODO:
The pairing of measures with test functions are called `test_against_nn` since the test functions
are taken to be `‚Ñù‚â•0`-valued in the definition of the topology (this allows us to use the Lebesgue
integral `lintegral`, but requires some coercions). It seems natural to later add `test_against`
for pairings via Bochner integrals (`integral`) with (bounded continuous) functions with values
in an arbitrary Banach spaces, especially with values in `‚Ñù` and `‚ÑÇ`. -/

variables {Œ± : Type} [measurable_space Œ±] [topological_space Œ±]

/-- The pairing of a (Borel) probability measure `Œº` with a nonnegative bounded continuous
function is obtained by (Lebesgue) integrating the (test) function against the measure. This
is `probability_measures.test_against'`. -/
abbreviation probability_measures.test_against_nn
  (Œº : probability_measures Œ±) (f : Œ± ‚Üí·µá nnreal) : ‚Ñù‚â•0 :=
(lintegral Œº.to_measure ((coe : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0‚àû) ‚àò f)).to_nnreal

lemma probability_measures.test_against_nn_def (Œº : probability_measures Œ±) {f : Œ± ‚Üí·µá nnreal} :
  Œº.test_against_nn f = (lintegral Œº.to_measure ((coe : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0‚àû) ‚àò f)).to_nnreal := by refl

/-- The pairing of a finite (Borel) measure `Œº` with a nonnegative bounded continuous
function is obtained by (Lebesgue) integrating the (test) function against the measure. This
is `finite_measures.test_against'`. -/
abbreviation finite_measures.test_against_nn
  (Œº : finite_measures Œ±) (f : Œ± ‚Üí·µá nnreal) : ‚Ñù‚â•0 :=
(lintegral Œº.to_measure ((coe : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0‚àû) ‚àò f)).to_nnreal

lemma finite_measures.test_against_nn_def (Œº : finite_measures Œ±) {f : Œ± ‚Üí·µá nnreal} :
  Œº.test_against_nn f = (lintegral Œº.to_measure ((coe : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0‚àû) ‚àò f)).to_nnreal := by refl

lemma finite_measures.test_against_nn_coe_eq {Œº : finite_measures Œ±} {f : Œ± ‚Üí·µá nnreal} :
  (Œº.test_against_nn f : ‚Ñù‚â•0‚àû) = lintegral Œº.to_measure ((coe : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0‚àû) ‚àò f) :=
begin
  have key_lt := lintegral_lt_top_of_bounded_continuous_to_nnreal Œº.to_measure f,
  exact ennreal.coe_to_nnreal (ennreal.lt_top_iff_ne_top.mp key_lt),
end

lemma probability_measures.test_against_nn_coe_eq {Œº : probability_measures Œ±} {f : Œ± ‚Üí·µá nnreal} :
  (Œº.test_against_nn f : ‚Ñù‚â•0‚àû) = lintegral Œº.to_measure ((coe : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0‚àû) ‚àò f) :=
begin
  have key_lt := lintegral_lt_top_of_bounded_continuous_to_nnreal Œº.to_measure f,
  exact ennreal.coe_to_nnreal (ennreal.lt_top_iff_ne_top.mp key_lt),
end

@[simp]
lemma probability_measures.to_finite_measure_test_against_nn_eq_test_against_nn (Œ± : Type*)
  [measurable_space Œ±] [topological_space Œ±] {Œº : probability_measures Œ±} {f : Œ± ‚Üí·µá nnreal} :
  Œº.to_finite_measure.test_against_nn f = Œº.test_against_nn f := rfl

lemma finite_measures.test_against_nn_const (Œº : finite_measures Œ±) (c : ‚Ñù‚â•0) :
  Œº.test_against_nn (bounded_continuous_function.const Œ± c) = c * Œº.mass :=
begin
  rw finite_measures.test_against_nn_def,
  have eq : (coe : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0‚àû) ‚àò (bounded_continuous_function.const Œ± c) = (Œª x, (c : ennreal)),
  by refl,
  rw [eq, lintegral_const, ennreal.to_nnreal_mul],
  simp only [mul_eq_mul_left_iff, ennreal.to_nnreal_coe, finite_measures.mass_def],
  left,
  refl,
end

lemma probability_measures.test_against_nn_const (Œº : probability_measures Œ±) (c : ‚Ñù‚â•0) :
  Œº.test_against_nn (bounded_continuous_function.const Œ± c) = c :=
begin
  have key := finite_measures.test_against_nn_const Œº.to_finite_measure c,
  simp only [mul_one, probability_measures.to_finite_measure_mass] at key,
  exact key,
end

lemma finite_measures.test_against_nn_mono (Œº : finite_measures Œ±)
  {f g : Œ± ‚Üí·µá ‚Ñù‚â•0} (f_le_g : (f : Œ± ‚Üí ‚Ñù‚â•0) ‚â§ g) :
  Œº.test_against_nn f ‚â§ Œº.test_against_nn g :=
begin
  repeat { rw finite_measures.test_against_nn_def, },
  apply ennreal.coe_le_coe.mp,
  repeat { rw ennreal.coe_to_nnreal, },
  { apply lintegral_mono,
    intros x,
    apply ennreal.coe_mono, 
    exact f_le_g x, },
  repeat { apply ennreal.lt_top_iff_ne_top.mp,
           apply lintegral_lt_top_of_bounded_continuous_to_nnreal, },
end

lemma probability_measures.test_against_nn_mono (Œº : probability_measures Œ±)
  {f g : Œ± ‚Üí·µá ‚Ñù‚â•0} (f_le_g : (f : Œ± ‚Üí ‚Ñù‚â•0) ‚â§ g) :
  Œº.test_against_nn f ‚â§ Œº.test_against_nn g :=
begin
  have key := finite_measures.test_against_nn_mono Œº.to_finite_measure f_le_g,
  simp only [probability_measures.to_finite_measure_test_against_nn_eq_test_against_nn] at key,
  exact key,
end

lemma continuous_bounded_nn_add_comp_coe {Œ≤ : Type*} [topological_space Œ≤] {f‚ÇÅ f‚ÇÇ : Œ≤ ‚Üí·µá ‚Ñù‚â•0} :
  (coe : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0‚àû) ‚àò (f‚ÇÅ + f‚ÇÇ) = ( ((coe : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0‚àû) ‚àò f‚ÇÅ) + ((coe : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0‚àû) ‚àò f‚ÇÇ)) :=
by { funext x, simp only [ennreal.coe_add, pi.add_apply, function.comp_app], }

lemma continuous_bounded_nn_smul_comp_coe {Œ≤ : Type*} [topological_space Œ≤] {c : ‚Ñù‚â•0}
  {f : Œ≤ ‚Üí·µá ‚Ñù‚â•0} :
  (coe : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0‚àû) ‚àò (c ‚Ä¢ f) = c ‚Ä¢ ( ((coe : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0‚àû) ‚àò f)) :=
begin
  funext x,
  simpa only [algebra.id.smul_eq_mul, function.comp_app, pi.smul_apply, ennreal.coe_mul],
end

lemma continuous_bounded_nn_smul_eq {Œ≤ : Type*} [topological_space Œ≤] {c : ‚Ñù‚â•0} {f : Œ≤ ‚Üí·µá ‚Ñù‚â•0} :
  (c ‚Ä¢ (f : Œ≤ ‚Üí ‚Ñù‚â•0)) = ((c ‚Ä¢ f) : Œ≤ ‚Üí·µá ‚Ñù‚â•0) := by refl

lemma continuous_bounded_nn_smul_comp_coe' {Œ≤ : Type*} [topological_space Œ≤] {c : ‚Ñù‚â•0} {f : Œ≤ ‚Üí·µá ‚Ñù‚â•0} :
  (coe : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0‚àû) ‚àò (c ‚Ä¢ f) = c ‚Ä¢ ( ((coe : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0‚àû) ‚àò f)) :=
begin
  funext x,
  simpa only [algebra.id.smul_eq_mul, function.comp_app, pi.smul_apply, ennreal.coe_mul],
end

variables [opens_measurable_space Œ±] -- [borel_space Œ±]

lemma finite_measures.test_against_nn_add (Œº : finite_measures Œ±) (f‚ÇÅ f‚ÇÇ : Œ± ‚Üí·µá ‚Ñù‚â•0) :
  Œº.test_against_nn (f‚ÇÅ + f‚ÇÇ) = Œº.test_against_nn f‚ÇÅ + Œº.test_against_nn f‚ÇÇ :=
begin
  rw ‚Üê ennreal.to_nnreal_add
    (lintegral_lt_top_of_bounded_continuous_to_nnreal Œº.to_measure f‚ÇÅ)
    (lintegral_lt_top_of_bounded_continuous_to_nnreal Œº.to_measure f‚ÇÇ),
  rw ‚Üê @lintegral_add _ _ Œº.to_measure _ _
    f‚ÇÅ.coe_nnreal_comp_measurable f‚ÇÇ.coe_nnreal_comp_measurable,
  refl,
end

lemma finite_measures.test_against_nn_smul (Œº : finite_measures Œ±) (c : ‚Ñù‚â•0) (f : Œ± ‚Üí·µá ‚Ñù‚â•0) :
  Œº.test_against_nn (c ‚Ä¢ f) = c * Œº.test_against_nn f :=
begin
  have key_smul := @lintegral_mul_const _ _ Œº.to_measure c _ f.coe_nnreal_comp_measurable,
  simp_rw mul_comm at key_smul,
  repeat { rw finite_measures.test_against_nn_def, },
  rw [nnreal_mul_ennreal_to_nnreal, ‚Üêkey_smul],
  rw [‚Üêcontinuous_bounded_nn_smul_eq, @continuous_bounded_nn_smul_comp_coe _ _ c f],
  refl,
end

/-- Integration against a finite_measure defines a linear map from nonnegative bounded continuous
functions to nonnegative real numbers. -/
def finite_measures.test_against_nn_linear_map (Œº : finite_measures Œ±) :
  linear_map ‚Ñù‚â•0 (Œ± ‚Üí·µá ‚Ñù‚â•0) ‚Ñù‚â•0 :=
{ to_fun := Œº.test_against_nn,
  map_add' := finite_measures.test_against_nn_add Œº,
  map_smul' := finite_measures.test_against_nn_smul Œº, }

lemma nnreal.le_add_dist (a b : ‚Ñù‚â•0) : a ‚â§ b + (dist a b).to_nnreal :=
begin
  suffices : (a : ‚Ñù) ‚â§ (b : ‚Ñù) + (dist a b),
  { have coe_eq : (coe : ‚Ñù‚â•0 ‚Üí ‚Ñù) (b + (dist a b).to_nnreal) = (b : ‚Ñù) + dist a b,
    { rw nnreal.coe_add,
      simp only [real.coe_to_nnreal', max_eq_left_iff, add_right_inj],
      exact dist_nonneg, },
    rw ‚Üêcoe_eq at this,
    apply nnreal.coe_le_coe.mp this, },
  have key : abs (a-b : ‚Ñù) ‚â§ (dist a b) := by refl,
  linarith [le_of_abs_le key],
end

lemma finite_measures.lipschitz_estimate (Œº : finite_measures Œ±) (f g : Œ± ‚Üí·µá ‚Ñù‚â•0) :
  Œº.test_against_nn f ‚â§ Œº.test_against_nn g + (dist f g).to_nnreal * Œº.mass :=
begin
  have coe_eq : ennreal.of_real (dist f g) = ((dist f g).to_nnreal : ‚Ñù‚â•0‚àû) := by refl,
  rw ‚Üêfinite_measures.test_against_nn_const Œº (dist f g).to_nnreal,
  rw ‚Üêfinite_measures.test_against_nn_add,
  repeat { rw finite_measures.test_against_nn_def, },
  apply ennreal.coe_le_coe.mp,
  repeat { rw ennreal.coe_to_nnreal, },
  { apply lintegral_mono,
    have le_dist : ‚àÄ x, dist (f x) (g x) ‚â§ (dist f g)
    := bounded_continuous_function.dist_coe_le_dist,
    have le' : ‚àÄ x, f(x) ‚â§ g(x) + (dist f g).to_nnreal,
    { intros x,
      apply (nnreal.le_add_dist (f x) (g x)).trans,
      rw add_le_add_iff_left,
      exact real.to_nnreal_mono (le_dist x), },
    have le : ‚àÄ x, (f(x) : ‚Ñù‚â•0‚àû) ‚â§ (g(x) : ‚Ñù‚â•0‚àû) + ennreal.of_real (dist f g),
    { intros x,
      rw [coe_eq, ‚Üêennreal.coe_add],
      exact ennreal.coe_mono (le' x), },
    exact le, },
  repeat { apply ennreal.lt_top_iff_ne_top.mp,
           apply lintegral_lt_top_of_bounded_continuous_to_nnreal, },
end

lemma finite_measures.test_against_nn_lipschitz (Œº : finite_measures Œ±) :
  lipschitz_with Œº.mass Œº.test_against_nn :=
begin
  rw lipschitz_with_iff_dist_le_mul,
  intros f‚ÇÅ f‚ÇÇ,
  suffices : abs (Œº.test_against_nn f‚ÇÅ - Œº.test_against_nn f‚ÇÇ : ‚Ñù) ‚â§ Œº.mass * (dist f‚ÇÅ f‚ÇÇ),
  { rwa nnreal.dist_eq, },
  apply (@abs_le ‚Ñù _ _ _ _ _).mpr,
  split,
  { have key' := Œº.lipschitz_estimate f‚ÇÇ f‚ÇÅ,
    rw mul_comm at key',
    suffices : ‚Üë(Œº.test_against_nn f‚ÇÇ) ‚â§ ‚Üë(Œº.test_against_nn f‚ÇÅ) + ‚Üë(Œº.mass) * dist f‚ÇÅ f‚ÇÇ,
    { linarith, },
    have key := nnreal.coe_mono key',
    rwa [nnreal.coe_add, nnreal.coe_mul, real.coe_to_nnreal, dist_comm] at key,
    exact dist_nonneg, },
  { have key' := Œº.lipschitz_estimate f‚ÇÅ f‚ÇÇ,
    rw mul_comm at key',
    suffices : ‚Üë(Œº.test_against_nn f‚ÇÅ) ‚â§ ‚Üë(Œº.test_against_nn f‚ÇÇ) + ‚Üë(Œº.mass) * dist f‚ÇÅ f‚ÇÇ,
    { linarith, },
    have key := nnreal.coe_mono key',
    rwa [nnreal.coe_add, nnreal.coe_mul, real.coe_to_nnreal] at key,
    exact dist_nonneg, },
end

lemma probability_measures.test_against_nn_lipschitz (Œº : probability_measures Œ±) :
  lipschitz_with 1 Œº.test_against_nn :=
begin
  have key := Œº.to_finite_measure.test_against_nn_lipschitz,
  rwa Œº.to_finite_measure_mass at key,
end

/-- A finite measure can be interpreted as an element of the (weak) dual of nonnegative
bounded continuous functions, the duality pairing being integration. -/
def finite_measures.to_weak_dual_of_bcnn (Œº : finite_measures Œ±) :
  weak_dual ‚Ñù‚â•0 (Œ± ‚Üí·µá ‚Ñù‚â•0) :=
{ to_fun := Œº.test_against_nn,
  map_add' := finite_measures.test_against_nn_add Œº,
  map_smul' := finite_measures.test_against_nn_smul Œº,
  cont := Œº.test_against_nn_lipschitz.continuous, }

/-
--TODO: Need `‚Ñù‚â•0`-module structure on `finite_measures Œ±`.
--      Currently only `add_comm_monoid` is implemented.
def finite_measures.to_weak_dual_of_bounded_continuous_nn :
  finite_measures Œ± ‚ÜíL[‚Ñù‚â•0] weak_dual ‚Ñù‚â•0 (Œ± ‚Üí·µá ‚Ñù‚â•0) := sorry
-/

/-- A probability measure can be interpreted as an element of the (weak) dual of nonnegative
bounded continuous functions (random variables), the duality pairing being integration (expected
value). -/
def probability_measures.to_weak_dual_of_bcnn (Œº : probability_measures Œ±) :
  weak_dual ‚Ñù‚â•0 (Œ± ‚Üí·µá ‚Ñù‚â•0) :=
{ to_fun := Œº.test_against_nn,
  map_add' := Œº.to_finite_measure.test_against_nn_add,
  map_smul' := Œº.to_finite_measure.test_against_nn_smul,
  cont := Œº.test_against_nn_lipschitz.continuous, }

lemma finite_measures.test_against_eq_to_weak_dual_test (Œº : finite_measures Œ±) :
  (Œº.to_weak_dual_of_bcnn : (Œ± ‚Üí·µá ‚Ñù‚â•0) ‚Üí ‚Ñù‚â•0) = Œº.test_against_nn := by refl

lemma probability_measures.test_against_eq_to_weak_dual_test (Œº : probability_measures Œ±) :
  (Œº.to_weak_dual_of_bcnn : (Œ± ‚Üí·µá ‚Ñù‚â•0) ‚Üí ‚Ñù‚â•0) = Œº.test_against_nn := by refl

/-- The topology of weak convergence on `finite_measures Œ±` is inherited (induced) from the weak-*
topology on `weak_dual ‚Ñù‚â•0 (Œ± ‚Üí·µá ‚Ñù‚â•0)` via the function `finite_measures.to_weak_dual_of_bcnn`. -/
instance : topological_space (finite_measures Œ±) :=
topological_space.induced
  (Œª (Œº : finite_measures Œ±), Œº.to_weak_dual_of_bcnn) infer_instance

/-- The topology of weak convergence on `probability_measures Œ±`. This is inherited (induced) from
the weak-*  topology on `weak_dual ‚Ñù‚â•0 (Œ± ‚Üí·µá ‚Ñù‚â•0)` via the function 
`probability_measures.to_weak_dual_of_bcnn`. -/
instance : topological_space (probability_measures Œ±) :=
topological_space.induced
  (Œª (Œº : probability_measures Œ±), Œº.to_weak_dual_of_bcnn) infer_instance

/- Integration of (nonnegative bounded continuous) test functions against finite Borel measures
depends continuously on the measure. -/
lemma finite_measures.to_weak_dual_continuous :
  continuous (@finite_measures.to_weak_dual_of_bcnn Œ± _ _ _) :=
continuous_induced_dom

/- Integration of (nonnegative bounded continuous) test functions against Borel probability
measures depends continuously on the measure. -/
lemma probability_measures.to_weak_dual_continuous :
  continuous (@probability_measures.to_weak_dual_of_bcnn Œ± _ _ _) :=
continuous_induced_dom

/- The canonical mapping from probability measures to finite measures is an embedding. -/
lemma probability_measures.coe_embedding (Œ± : Type*)
  [measurable_space Œ±] [topological_space Œ±] [opens_measurable_space Œ±] :
  embedding (coe : probability_measures Œ± ‚Üí finite_measures Œ±) :=
{ induced := begin
    have key := @induced_compose (probability_measures Œ±) (finite_measures Œ±) _ _ coe
      (@finite_measures.to_weak_dual_of_bcnn Œ± _ _ _),
    exact key.symm,
  end,
  inj := begin
    intros Œº ŒΩ h,
    apply subtype.eq,
    rw [‚ÜêŒº.to_finite_measure_to_measure_eq_val, ‚ÜêŒΩ.to_finite_measure_to_measure_eq_val],
    apply congr_arg _ h,
  end, }

lemma probability_measures.tendsto_nhds_iff_to_finite_measures_tendsto_nhds {Œ¥ : Type*}
  (F : filter Œ¥) {Œºs : Œ¥ ‚Üí probability_measures Œ±} {Œº‚ÇÄ : probability_measures Œ±} :
  tendsto Œºs F (ùìù Œº‚ÇÄ) ‚Üî tendsto (coe ‚àò Œºs) F (ùìù (Œº‚ÇÄ.to_finite_measure)) :=
embedding.tendsto_nhds_iff (probability_measures.coe_embedding Œ±)

lemma finite_measures.tendsto_iff_weak_star_tendsto {Œ≥ : Type*} {F : filter Œ≥}
  {Œºs : Œ≥ ‚Üí finite_measures Œ±} {Œº : finite_measures Œ±} :
  tendsto Œºs F (ùìù Œº) ‚Üî
    tendsto (Œª i, (Œºs(i)).to_weak_dual_of_bcnn)
      F (ùìù Œº.to_weak_dual_of_bcnn) :=
by apply tendsto_induced_iff

theorem finite_measures.tendsto_iff_forall_test_against_nn_tendsto {Œ≥ : Type*} {F : filter Œ≥}
  {Œºs : Œ≥ ‚Üí finite_measures Œ±} {Œº : finite_measures Œ±} :
  tendsto Œºs F (ùìù Œº) ‚Üî
  ‚àÄ (f : Œ± ‚Üí·µá ‚Ñù‚â•0),
    tendsto (Œª i, (Œºs(i)).to_weak_dual_of_bcnn f)
      F (ùìù (Œº.to_weak_dual_of_bcnn f)) :=
by rw [finite_measures.tendsto_iff_weak_star_tendsto,
       weak_dual.tendsto_iff_forall_test_tendsto]

theorem finite_measures.tendsto_iff_forall_lintegral_tendsto {Œ≥ : Type*} {F : filter Œ≥}
  {Œºs : Œ≥ ‚Üí finite_measures Œ±} {Œº : finite_measures Œ±} :
  tendsto Œºs F (ùìù Œº) ‚Üî
  ‚àÄ (f : Œ± ‚Üí·µá ‚Ñù‚â•0),
    tendsto (Œª i, lintegral (Œºs(i)).to_measure (coe ‚àò f)) F (ùìù (lintegral Œº.to_measure (coe ‚àò f))) :=
begin
  rw finite_measures.tendsto_iff_forall_test_against_nn_tendsto,
  simp_rw finite_measures.test_against_eq_to_weak_dual_test,
  simp_rw ‚Üêfinite_measures.test_against_nn_coe_eq,
  simp_rw ennreal.tendsto_coe,
end

/-- The usual definition of weak convergence of probability measures is given in terms of sequences
of probability measures: it is the requirement that the integrals of all continuous bounded
functions against members of the sequence converge. This version is a characterization using
nonnegative bounded continuous functions. -/
theorem probability_measures.tendsto_iff_forall_lintegral_tendsto {Œ≥ : Type*} {F : filter Œ≥}
  {Œºs : Œ≥ ‚Üí probability_measures Œ±} {Œº : probability_measures Œ±} :
  tendsto Œºs F (ùìù Œº) ‚Üî
  ‚àÄ (f : Œ± ‚Üí·µá ‚Ñù‚â•0),
    tendsto (Œª i, lintegral (Œºs(i)).to_measure (coe ‚àò f)) F (ùìù (lintegral Œº.to_measure (coe ‚àò f))) :=
begin
  rw [probability_measures.tendsto_nhds_iff_to_finite_measures_tendsto_nhds,
      finite_measures.tendsto_iff_forall_lintegral_tendsto],
  refl,
end

end weak_convergence_of_measures
